apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'


group = 'cz.siret'
version = '2.1-dev.2'

description = 'Ligand binding site prediction based on machine learning.'


allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {

    jcenter()
    mavenCentral()
    mavenLocal()

	maven {
		url = "http://www.biojava.org/download/maven/"
	}
    maven {
		url = "https://maven.ch.cam.ac.uk/"
	}

    flatDir(dirs: 'lib')
}

sourceSets {
    main {
        resources {
            exclude 'scripts/out/*'
            exclude 'models/*'
        }
    }
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            "project_version": project.version
    ]
}

task(console, dependsOn: 'classes', type: JavaExec) {
    main = 'groovy.ui.Console'
    classpath = sourceSets.test.runtimeClasspath
}

task copyDependenciesToDist( type: Copy ) {
    into "$buildDir/../distro/bin/lib"
    from configurations.runtime
}

task copyBinaryToDist( type: Copy ) {
    dependsOn jar

    into "$buildDir/../distro/bin"
    from "$buildDir/bin"
    include "*.jar"
}

assemble {
    dependsOn processResources
    dependsOn copyBinaryToDist
    dependsOn copyDependenciesToDist // copy dependencies to distro dir
}

jar {
    archiveName = "p2rank.jar"
    destinationDir = file("build/bin")

    manifest {
        attributes 'Main-Class': 'cz.siret.prank.program.Main'
    }
}

clean {
    delete "$buildDir/../distro/bin"
    delete "$buildDir/../distro/test_output"
    delete "$buildDir/../distro/log/prank.log"
}

test {

    // show standard out and standard error of the test JVM(s) on the console
    // testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "1512m"
    
    def distroDir = "$project.projectDir/distro"
    inputs.dir "$distroDir/test_data"
    inputs.files("$distroDir/models", "$distroDir/config/default.groovy")

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    //    onOutput { descriptor, event ->
    //        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    //    }
    
}


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.6'
    
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile "org.apache.commons:commons-lang3:3.7"
    compile "commons-io:commons-io:2.6"
    compile "com.google.guava:guava:24.0-jre"
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'org.zeroturnaround:zt-zip:1.12'

    compile 'com.univocity:univocity-parsers:2.4.0' // for parsing csv

    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.2' 
    compile 'org.apache.logging.log4j:log4j-core:2.11.2'
    compile 'org.slf4j:jul-to-slf4j:1.7.26'         // for netlib logging messages

    compile 'org.biojava:biojava-core:4.2.12'
    compile 'org.biojava:biojava-alignment:4.2.12'
    compile 'org.biojava:biojava-structure:4.2.12'

    compile 'javax.xml.bind:jaxb-api:2.2.11'      // extra dependencies of biojava for java 11
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
    compile 'javax.activation:activation:1.1.1'

    compile 'org.openscience.cdk:cdk-qsarmolecular:2.1.1'    // for NumericalSurface class
    compile 'org.openscience.cdk:cdk-data:2.1.1'             // for Atom class
    compile 'nz.ac.waikato.cms.weka:weka-dev:3.9.2'
    compile fileTree(dir: 'lib', include: '*.jar')

    testCompile 'junit:junit:4.12'
}

